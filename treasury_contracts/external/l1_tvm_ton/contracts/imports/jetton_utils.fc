#include "utils.fc";

cell pack_jetton_wallet_data(int balance, slice owner_address, slice jetton_master_address) inline {
  return begin_cell()
        .store_coins(balance)
        .store_slice(owner_address)
        .store_slice(jetton_master_address)
        .end_cell();
}

cell calculate_jetton_wallet_state_init(slice owner_address, slice jetton_master_address, cell jetton_wallet_code) inline {
  return begin_cell()
        .store_uint(0, 2)
        .store_dict(jetton_wallet_code)
        .store_dict(pack_jetton_wallet_data(0, owner_address, jetton_master_address))
        .store_uint(0, 1)
        .end_cell();
}
  
slice calculate_user_jetton_wallet_address(slice owner_address, slice jetton_master_address, cell jetton_wallet_code) inline {
  return calculate_address_by_state_init(BASECHAIN, calculate_jetton_wallet_state_init(owner_address, jetton_master_address, jetton_wallet_code));
}
